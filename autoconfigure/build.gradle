plugins {
    id "checkstyle"
    id "jacoco"
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
    id "io.freefair.lombok" version "2.6"
}
apply plugin: "io.freefair.maven-jars"
apply plugin: "io.freefair.javadoc-links"
apply plugin: "io.freefair.maven-publish-java"
apply plugin: "io.freefair.maven-optional"

publishing.publications.mavenJava.artifactId = "okhttp-spring-boot-autoconfigure"

javadoc.options.linkSource true

checkstyle.toolVersion = "8.8"

if (JavaVersion.current().isJava11Compatible()) {
    jacoco.toolVersion = "0.8.2-SNAPSHOT"
    javadoc.failOnError = false
}

compileJava.dependsOn(processResources)

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:2.1.0.M3'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

jacocoTestReport.reports.xml.enabled = true

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'

    compile 'org.springframework.boot:spring-boot-autoconfigure'
    compile 'org.slf4j:slf4j-api'

    optional 'org.springframework:spring-web'

    optional 'com.squareup.okhttp3:okhttp:3.+'
    optional 'com.squareup.okhttp3:logging-interceptor:3.+'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
}
